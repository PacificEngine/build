plugins {
    id 'maven-publish'
    id 'signing'
}

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
    signing {
        useGpgCmd()
        sign publishing.publications.pluginMaven ?: publishing.publications.rootMaven
    }
}

group = project.property('project.group')
version = project.property('project.version')

publishing {
    repositories {
        if((project.findProperty('git.maven.repo.url') != null && project.findProperty('project.name.short') != null)
                || System.getenv('GIT_MAVEN_URL') != null) {
            maven {
                name = "GitHubPackages"
                url = System.getenv('GIT_MAVEN_URL') ?: "${project.property('git.maven.repo.url')}/${project.property('project.name.short')}"
                credentials {
                    username = System.getenv('GIT_USERNAME') ?: project.findProperty("git.maven.repo.user")
                    password = System.getenv('GIT_TOKEN') ?: project.findProperty("git.maven.repo.key")
                }
            }
        }
        if(project.findProperty('gcp.maven.repo.url') != null
                || System.getenv('GCP_MAVEN_URL') != null) {
            maven {
                name = "GCPPackages"
                url = System.getenv('GCP_MAVEN_URL') ?: project.findProperty('gcp.maven.repo.url')
                credentials {
                    username = System.getenv('GCP_MAVEN_USER') ?: project.findProperty('gcp.maven.repo.user')
                    password = System.getenv('GCP_MAVEN_KEY') ?: project.findProperty('gcp.maven.repo.key')
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}

afterEvaluate  {
    tasks.withType(PublishToMavenRepository) { task ->
        if (task.publication.name == "pluginMaven") {
            task.publication.groupId = project.property('project.group')
            task.publication.artifactId = "${project.name}-${rootProject.name}"
            task.publication.version = project.property('project.version')
        } else if (task.publication.name != "rootMaven") {
            logger.lifecycle("Disabling ${task.publication.name}")
            task.enabled = false
            task.group = null
        }
    }
}


