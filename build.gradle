plugins {
    id 'io.github.pacificengine.build.plugin.release'
}

ext {
    isReleaseVersion        = !version.endsWith("SNAPSHOT")
    pluginPublish_version   = '1.2.1'
}

publishing {
    publications {
        rootMaven(MavenPublication) {
            groupId = project.findProperty('project.group') ?: project.group
            artifactId = "all-gradle-plugin"
            version = project.findProperty('project.version') ?: project.version

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    dependencies {
                        rootProject.subprojects.forEach(proj -> {
                            if (proj.hasProperty('plugin.artifact.id')) {
                                dependency {
                                    groupId project.findProperty('project.group') ?: project.group
                                    artifactId proj.property('plugin.artifact.id')
                                    version project.findProperty('project.version') ?: project.version
                                }
                            }
                        })
                    }
                }
            }
        }
    }
}

task printVersion {
    doLast {
        println project.findProperty('project.version') ?: project.version
    }
}
