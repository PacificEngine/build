plugins {
    id 'groovy-gradle-plugin'
}

group = project.findProperty('project.group') ?: project.group ?: rootProject.findProperty('project.group') ?: rootProject.group
version = project.findProperty('project.version') ?: project.version ?: rootProject.findProperty('project.version') ?: rootProject.version

def gitSourceUrl = project.findProperty('git.source.repo.url') ?: "${project.findProperty('git.source.url')}/${project.findProperty('git.project.name') ?: project.findProperty('project.name.short')}"
gradlePlugin {
    website.set(project.findProperty('plugin.website.url') ?: project.findProperty('plugin.url') ?: project.findProperty('project.url') ?: gitSourceUrl)
    vcsUrl.set(project.findProperty('plugin.vcs.url') ?: project.findProperty('plugin.url') ?: gitSourceUrl)
    plugins.configureEach {
        if(project.findProperty('plugin.id') == null
                || project.findProperty('plugin.id') == ''
                || name == project.findProperty('plugin.id')) {
            displayName = project.findProperty('plugin.displayName') ?: project.findProperty('project.name') ?: project.name
            description = project.findProperty('plugin.description')
            implementationClass = project.findProperty('plugin.implementationClass')
            tags.set(project.findProperty('plugin.tags')?.split(';')?.toList() ?: List<String>.of())
        } else {
            logger.log(LogLevel.ERROR, "Unexpected Plugin Identifier (Skipping): ${name}")
            // Doesn't actually skip, it just errors because of lack of displayName
        }
    }
}